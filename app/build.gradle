apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// This handles both kotlin and java. Use kapt instead of annotationProcessor
apply plugin: 'kotlin-kapt'

// Update this to update the database on the next build
// This is also used to populate populate MHWDatabase.java configuration
def DATABASE_VERSION = 25

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gatheringhallstudios.mhworlddatabase"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 8
        versionName "1.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // We set this to false so vectors get rastered at buildtime.
        vectorDrawables.useSupportLibrary = false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("int", "DB_VERSION", "${DATABASE_VERSION}")
        }
        debug {
            buildConfigField("int", "DB_VERSION", "${DATABASE_VERSION}")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false

        // this application is open source, so we can't force translators to always translate.
        // disable it so we can at least fallback to english without explicit errors.
        disable 'MissingTranslation'
    }
}

androidExtensions {
    // Enables KTX viewbinding to work on viewholders as well
    experimental = true
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    /*
     * Google Support Libraries
     */
    def google_version = "28.0.0"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'androidx.interpolator:interpolator:1.0.0'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha2"
    implementation 'androidx.preference:preference:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1"

    /*
     * Leak Canary
     * https://github.com/square/leakcanary
     */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'

    /*
     * Android Kotlin Extensions
     */
    implementation "androidx.core:core-ktx:1.0.1"

    /*
     * Android Architecture Components
     */
    def architecture_version = "1.1.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    implementation 'androidx.room:room-runtime:2.1.0-alpha03'
    kapt 'androidx.room:room-compiler:2.1.0-alpha03'
    testImplementation 'androidx.arch.core:core-testing:2.0.0'
    testImplementation 'androidx.room:room-testing:2.1.0-alpha03'

    /*
     * Android Navigation Component
     */
    def nav_version = "1.0.0-alpha11"
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    /*
     * Butterknife
     * https://github.com/JakeWharton/butterknife
     */
    implementation 'com.jakewharton:butterknife:10.0.0'
    kapt 'com.jakewharton:butterknife-compiler:10.0.0'

    /*
     * Adapter Delegates
     * https://github.com/sockeqwe/AdapterDelegates
     */
    implementation 'com.hannesdorfmann:adapterdelegates4:4.0.0'

    /*
     * Groupie
     * https://github.com/lisawray/groupie
     */
    implementation 'com.xwray:groupie:2.3.0'
    implementation 'com.xwray:groupie:2.3.0'
    implementation 'com.xwray:groupie-kotlin-android-extensions:2.3.0'

    /*
     * VectorMaster
     * https://github.com/harjot-oberai/VectorMaster
     */
    implementation 'com.sdsmdg.harjot:vectormaster:1.1.3'

    /*
     * Testing
     */
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.1-beta01"
    androidTestImplementation 'androidx.test:rules:1.1.1-beta01'
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1-beta01"

    /*
    Change log extension
     */
    implementation 'com.github.MFlisar:changelog:1.1.2'

}

apply plugin: 'de.undercouch.download'

/**
 * Download the database every time the app is assembled if it hasn't been done already.
 */
gradle.projectsEvaluated {
    preBuild.dependsOn('downloadDatabase')
}

task downloadDatabase(type: Download){
    src "https://github.com/gatheringhallstudios/MHWorldData/releases/download/V${DATABASE_VERSION}/mhw.db"
    dest "src/main/assets/databases/mhw.db"
    overwrite true
    onlyIfNewer true
}
